// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id "com.google.protobuf" version "0.8.18" apply false
    id "com.diffplug.spotless" version "6.4.2" apply false
    id "org.sonarqube" version "3.3"
}

subprojects {
    def isAndroid = project.name in [ 'wizard-app' ]

    group = "com.github.wizard"
    version = "0.0.1-SNAPSHOT"

    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'jacoco'

    spotless {
        groovyGradle {
            target '**/*.gradle'
            greclipse()
            indentWithSpaces()
        }
        java {
            targetExclude("build/generated/source/proto/**/*.java")
            googleJavaFormat().aosp().reflowLongStrings()
        }
    }

    ext {
        protobufVersion = "3.20.0"
        grpcVersion = "1.45.0"

        configureProtobuf = {
            project.protobuf {
                protoc {
                    artifact = "com.google.protobuf:protoc:${protobufVersion}"
                }
                plugins {
                    grpc {
                        artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
                    }
                }
                generateProtoTasks {
                    if (isAndroid) {
                        all().each { task ->
                            task.builtins {
                                java { option 'lite' }
                            }
                            task.plugins {
                                grpc { option 'lite' }
                            }
                        }
                    } else {
                        all()*.plugins {
                            grpc {}
                        }
                    }
                }
            }
            project.sourceSets {
                main {
                    java {
                        srcDirs 'build/generated/source/proto/main/grpc'
                        srcDirs 'build/generated/source/proto/main/java'
                    }
                }
            }

            // Grpc dependencies
            project.dependencies {
                if (isAndroid) {
                    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
                    implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
                } else {
                    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
                    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
                }

                implementation "io.grpc:grpc-stub:${grpcVersion}"
                compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

                protobuf files("${project.rootDir}/protobuf")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "NewOrganizationTest123_test"
        property "sonar.organization", "neworganizationtest123"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
